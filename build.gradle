buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.net.minecraftforge.gradle:ForgeGradle:2.0.1"
    }
}

ext.moduleDeps = [ "api":       [ ],
               "core":      [ "api" ],
               "builders":  [ "core" ],
               "energy":    [ "core" ],
               "factory":   [ "core" ],
               "transport": [ "core" ],
               "silicon":   [ "core" ],
               "robotics":  [ "transport", "silicon" ]
             ]

subprojects {
    apply plugin: "net.minecraftforge.gradle.forge"
    apply plugin: "maven" // for uploading to a maven repo
    // apply plugin: "checkstyle" // Re-enable checkstyle after dev stuffs

    version = "7.2.0"
    group= "com.mod-buildcraft"
    archivesBaseName = "buildcraft-$project.name" // the name that all artifacts will use as a base.
    // Artifacts names follow this pattern: [baseName]-[appendix]-[version]-[classifier].[extension]

    minecraft {
        mappings = "stable_16"
        version = "1.8-11.14.3.1512" // McVersion-ForgeVersion     this variable is later changed to contain only the MC version, while the apiVersion variable is used for the forge version.  Yeah its stupid, and will be changed eventually.

        runDir = "run" // the directory for ForgeGradle to run Minecraft in

        // replacing stuff in the source
        replace "@VERSION@", project.version
        replace "@MC_VERSION@", version
    }

    sourceSets {
        main {
            java {
                srcDir "java"
            }
            resources {
                srcDir "resources"
            }
        }
    }

    dependencies {
        moduleDeps["$project.name"].each { dependency ->
            compile project(":" + dependency)
        }
    }
}

//dependencies {
//    testCompile "junit:junit:4.12"
//}
